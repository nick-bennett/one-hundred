/*
 * Copyright 2022 Nicholas Bennett. See LICENSE for software license terms.
 */
plugins {
    id 'com.github.johnrengelman.shadow' version "${shadowVersion}"
    id 'application'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(project.javaVersion)
    }
    withSourcesJar()
    withJavadocJar()
}

application {
    mainClass = 'com.nickbenn.onehundred.ConsoleMain'
}

repositories {
    mavenCentral()
}

dependencies {

    // Apache Commons Command-line Interface (CLI) library
    implementation "commons-cli:commons-cli:$commonsCliVersion"

    // JUnit5 unit testing libraries
    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

}

test {
    useJUnitPlatform()
}

clean {
    delete += file("$projectDir/docs/api")
}

javadoc {

    destinationDir = file("$projectDir/docs/api")

    //noinspection GroovyAssignabilityCheck
    options {
        //noinspection GrUnresolvedAccess
        links "https://docs.oracle.com/en/java/javase/${javaVersion}/docs/api/"
    }

}

task testsJar(type: Jar, dependsOn: testClasses) {
    //noinspection GroovyAssignabilityCheck
    archiveClassifier = 'tests'
    from sourceSets.test.output
}

assemble {
//    dependsOn installDist, test, jar, shadowJar, sourcesJar, javadocJar, testsJar
    dependsOn installDist, jar, shadowJar, sourcesJar, javadocJar
}